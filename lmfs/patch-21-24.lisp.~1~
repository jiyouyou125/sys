;;; -*- Mode: Lisp; Package: User; Base: 10.; Patch-File: T -*-
;;; Patch file for LMFS version 21.24
;;; Reason: Do disk I/O properly.  Also don't swap in read buffers.
;;; Written 12/28/81 20:52:04 by Moon,
;;; while running on Spaniel from band 5
;;; with System 78.40, ZMail 38.5, Symbolics 8.7, Tape 6.5, LMFS 21.23, Canon 9.8, microcode 841.



; From file diskio.LISP >LMFS POINTER:
#10R LMFS:(COMPILER-LET ((PACKAGE (PKG-FIND-PACKAGE "LMFS")))

(DEFUN WIRE-FILE-BUFFERS-AND-RQB (FOR-READ BUFFERS)
  (SI:WIRE-PAGE FILE-BUFFER-RQB)
  (STORE32 (+ (SYS:%PHYSICAL-ADDRESS FILE-BUFFER-RQB) 1 (// SYS:%DISK-RQ-CCW-LIST 2))
	   FILE-BUFFER-RQB SYS:%DISK-RQ-CCW-LIST-POINTER-LOW)
  (LOOP FOR BUFFER IN BUFFERS
	WITH CCWX = SYS:%DISK-RQ-CCW-LIST
	DO (OR (AND (ARRAYP BUFFER) (ARRAY-DISPLACED-P BUFFER))
	       (FERROR NIL "~S is not a file-buffer" BUFFER))
	AS LOC = (%P-CONTENTS-OFFSET BUFFER 1)
	AS NPG = (// (ARRAY-LENGTH BUFFER)
		     (* (ARRAY-ELEMENTS-PER-Q (%P-LDB SYS:%%ARRAY-TYPE-FIELD BUFFER))
			SYS:PAGE-SIZE))
	DO (LOOP REPEAT NPG
		 DO (SI:WIRE-PAGE LOC T FOR-READ FOR-READ)
		    (STORE32 (1+ (SYS:%PHYSICAL-ADDRESS LOC)) FILE-BUFFER-RQB CCWX)
		    (INCF CCWX 2)
		    (INCF LOC SYS:PAGE-SIZE))
	FINALLY
	 (DECF CCWX 2)			;Turn off chain bit in last CCW
	 (ASET (LOGAND (AREF FILE-BUFFER-RQB CCWX) -2) FILE-BUFFER-RQB CCWX)))

)

; From file diskio.LISP >LMFS POINTER:
#10R LMFS:(COMPILER-LET ((PACKAGE (PKG-FIND-PACKAGE "LMFS")))


;The caller of these is responsible for locking so that only one operation is done
;at a time, since there is only one RQB.
(DEFUN FILE-DISK-READ (UNIT ADDRESS &REST BUFFERS)
  (WIRE-FILE-BUFFERS-AND-RQB T BUFFERS)
  (SI:DISK-READ-WIRED FILE-BUFFER-RQB UNIT ADDRESS)
  (UNWIRE-FILE-BUFFERS-AND-RQB BUFFERS))

(DEFUN FILE-DISK-WRITE (UNIT ADDRESS &REST BUFFERS)
  (WIRE-FILE-BUFFERS-AND-RQB NIL BUFFERS)
  (SI:DISK-WRITE-WIRED FILE-BUFFER-RQB UNIT ADDRESS)
  (UNWIRE-FILE-BUFFERS-AND-RQB BUFFERS))

(DEFUN FILE-DISK-READ-COMPARE (UNIT ADDRESS &REST BUFFERS)
  (WIRE-FILE-BUFFERS-AND-RQB NIL BUFFERS)
  (PROG1 (SI:DISK-READ-COMPARE-WIRED FILE-BUFFER-RQB UNIT ADDRESS)
	 (UNWIRE-FILE-BUFFERS-AND-RQB BUFFERS)))

)
