;;; -*- Mode:LISP; Package:TAPE -*-
;;; Band transfer to magtape
;;; * (c) Copyright 1981 Symbolics, Inc.

;;; A band consists of two "files".
;;; The first has ASCII info
;;; The second is the actual data of the partition.

(DEFUN WRITE-BAND-TO-TAPE (PARTITION &OPTIONAL (UNIT 0)
			   &AUX CONN STREAM TEM RQB BUF (QUANTUM 17.) NB TOP
				PART-BASE ORIG-PART-BASE PART-SIZE PART-COMMENT
				TAPE-P
				(N-HUNDRED 0))
  (IF (FIXP PARTITION) (SETQ PARTITION (FORMAT NIL "LOD~D" PARTITION)))
  (SETQ TAPE-P
	(FQUERY FORMAT:YES-OR-NO-QUIETLY-P-OPTIONS
		"Shall I use the local tape drive? (NO means listen on CHAOS) "))
  (UNWIND-PROTECT
    (PROGN
      (MULTIPLE-VALUE (PART-BASE PART-SIZE)
	(FIND-DISK-PARTITION PARTITION NIL UNIT))
      (OR PART-BASE		;***Change to FIND-DISK-PARTITION-FOR-READ in next system
	  (FERROR NIL "I see no /"~A/" partition here." PARTITION))
      (SETQ PART-SIZE (SI:MEASURED-SIZE-OF-PARTITION PARTITION UNIT)
	    PART-COMMENT (SI:PARTITION-COMMENT PARTITION UNIT))
      (IF TAPE-P
	  (SETQ STREAM (OPEN-TAPE 0 ':MODE ':WRITE ':RECORD-LENGTH 4096.))
	  (PROGN
	    (SETQ CONN (CHAOS:LISTEN "TAPE"))
	    (AND (STRINGP CONN) (FERROR NIL "Cannot connect: ~A" CONN))
	    (CHAOS:ACCEPT CONN)
	    (SETQ STREAM (CHAOS:STREAM CONN))))
      (FORMAT T "~&~:[Transmitting~;Writing~] ~A: ~D blocks, ~A~%"
	      TAPE-P PARTITION PART-SIZE PART-COMMENT)
      ;; First the partition description
      (FORMAT STREAM "VERSION 1~%DISTRIBUTION-TAPE~%")
      (FORMAT STREAM "PARTITION ~A~%" (STRING-TRIM " " PARTITION))
      (FORMAT STREAM "DISK-UNIT ~D~%" UNIT)
      (FORMAT STREAM "PARTITION-COMMENT ~A~%" PART-COMMENT)
      (FORMAT STREAM "DUMP-TIME ~A~%" (TIME:PRINT-UNIVERSAL-TIME
					(TIME:GET-UNIVERSAL-TIME) NIL))
      (FORMAT STREAM "USER-ID ~A~%" USER-ID)
      (FORMAT STREAM "SITE ~A~%" (SI:GET-SITE-OPTION ':SITE-PRETTY-NAME))
      (FORMAT STREAM "MACHINE ~A~%" (CHAOS:HOST-DATA))
      (FORMAT STREAM "PART-SIZE ~D~%" PART-SIZE)
      (FORMAT STREAM "END~%")
      (FUNCALL STREAM ':EOF)
      ;; Then the actual data
      (SETQ ORIG-PART-BASE PART-BASE)
      (SETQ RQB (GET-DISK-RQB QUANTUM)
	    BUF (RQB-BUFFER RQB))
      (SETQ SI:DISK-ERROR-RETRY-COUNT 20.)
      (SI:WIRE-DISK-RQB RQB)
      (SETQ TOP (+ PART-BASE PART-SIZE))
      (DO BLOCK PART-BASE (+ BLOCK QUANTUM) ( BLOCK TOP)
	  (AND (< (SETQ NB (- TOP BLOCK)) QUANTUM)
	       (SI:WIRE-DISK-RQB RQB (SETQ QUANTUM NB)))
	  (AND ( (SETQ TEM (// (- BLOCK ORIG-PART-BASE) 100.)) N-HUNDRED)
	       (FORMAT T "~D " (SETQ N-HUNDRED TEM)))
	  (SI:DISK-READ-WIRED RQB UNIT BLOCK)
	  (FUNCALL STREAM ':STRING-OUT
		   (SI:RQB-8-BIT-BUFFER RQB) 0 (* QUANTUM PAGE-SIZE 4)))
      (FUNCALL STREAM ':EOF)
      (FUNCALL STREAM ':CLOSE)
      (SETQ STREAM NIL))
    (AND RQB (RETURN-DISK-RQB RQB))
    (AND TAPE-P STREAM (FUNCALL STREAM ':CLOSE))
    (AND CONN (NOT (STRINGP CONN)) (CHAOS:REMOVE-CONN CONN))))


(DEFCONST *TAPE-BAND-RELOADER-KEYWORDS*
	  '(((ZWEI:PARSE-NUMBER LMFS:RELOADER-DEC-PRINT)
	     :PART-SIZE)
	    ((SUBSTRING PRINC)
	     :PARTITION :PARTITION-COMMENT)))

(DEFUN READ-BAND-FROM-TAPE (PARTITION &OPTIONAL (UNIT 0)
			    &AUX CONN STREAM TAPE-P
			         PART-SIZE PARTITION-COMMENT PART-BASE)
  (IF (FIXP PARTITION) (SETQ PARTITION (FORMAT NIL "LOD~D" PARTITION)))
  (SETQ TAPE-P
	(FQUERY FORMAT:YES-OR-NO-QUIETLY-P-OPTIONS
		"Shall I use the local tape drive? (NO means listen on CHAOS) "))
  (MULTIPLE-VALUE (PART-BASE PART-SIZE) (FIND-DISK-PARTITION PARTITION NIL UNIT))
  (OR PART-BASE		;***Change to FIND-DISK-PARTITION-FOR-WRITE in next system
      (FERROR NIL "No /"~A/" partition here." PARTITION))
  (IF (FQUERY FORMAT:YES-OR-NO-QUIETLY-P-OPTIONS
	      "Do you really want to clobber partition ~A (~A)? "
	      PARTITION (SI:PARTITION-COMMENT PARTITION UNIT))
      (UNWIND-PROTECT
	(PROGN
	  (IF TAPE-P
	      (SETQ STREAM (OPEN-TAPE 0 ':RECORD-LENGTH 4096.))
	      (PROGN
		(SETQ CONN (CHAOS:LISTEN "TAPE"))
		(AND (STRINGP CONN) (FERROR NIL "Cannot connect: ~A" CONN))
		(CHAOS:ACCEPT CONN)
		(SETQ STREAM (CHAOS:STREAM CONN))))
	  (LET ((STORY (LET ((LMFS:*BACKUP-KEYWORDS*
			       (APPEND *TAPE-BAND-RELOADER-KEYWORDS* LMFS:*BACKUP-KEYWORDS*)))
			 (LMFS:RELOADER-READ-TAPE-STORY STREAM T))))
	  ;; First get the size
	    (IF (NOT (AND (GET STORY ':PARTITION) (GETL STORY '(:DISTRIBUTION-TAPE))))
		(FERROR NIL "This does not appear to be a distribution tape."))
	    (LET ((TEM (OR (GET STORY ':PART-SIZE)
			   (FERROR NIL "Partition size not given on tape."))))
	      (OR ( TEM PART-SIZE)
		  (FERROR NIL "Does not fit in local partition, ~D>~D" TEM PART-SIZE))
	      (SETQ PART-SIZE TEM))
	    (FORMAT T "~&Tape written by ~A on machine ~A ~@[at ~A ~]at ~A."
		    (GET STORY ':USER-ID) (GET STORY ':MACHINE) (GET STORY ':SITE)
		    (TIME:PRINT-UNIVERSAL-TIME (GET STORY ':DUMP-TIME) NIL))
	    ;; Then the comment
	    (SETQ PARTITION-COMMENT (GET STORY ':PARTITION-COMMENT)))
	  (STREAM-COPY-UNTIL-EOF STREAM 'IGNORE)
	  (FUNCALL STREAM ':CLEAR-EOF)
	  (WRITE-BAND UNIT
		      PARTITION STREAM PART-SIZE PART-BASE PARTITION-COMMENT))
	(AND TAPE-P STREAM (FUNCALL STREAM ':CLOSE))
	(AND CONN (NOT (STRINGP CONN)) (CHAOS:REMOVE-CONN CONN)))))

(DEFUN WRITE-BAND (UNIT PARTITION STREAM PART-SIZE PART-BASE PARTITION-COMMENT
		   &AUX RQB BUF TEM (QUANTUM 17.) ORIG-PART-BASE (N-HUNDRED 0)
		   TOP NB)
  (UNWIND-PROTECT
    (PROGN
      (FORMAT T "~&Receiving ~A: ~D blocks, ~A~%"
	      PARTITION PART-SIZE PARTITION-COMMENT)
      
      ;; Now get the actual data
      (SI:UPDATE-PARTITION-COMMENT PARTITION "Incomplete Copy" UNIT)
      (SETQ ORIG-PART-BASE PART-BASE)
      (SETQ RQB (GET-DISK-RQB QUANTUM)
	    BUF (SI:RQB-8-BIT-BUFFER RQB))
      (SETQ SI:DISK-ERROR-RETRY-COUNT 20.)
      (SI:WIRE-DISK-RQB RQB)
      (SETQ TOP (+ PART-BASE PART-SIZE))
      (DO ((BLOCK PART-BASE (+ BLOCK QUANTUM)))
	  (( BLOCK TOP))
	(AND (< (SETQ NB (- TOP BLOCK)) QUANTUM)
	     (SI:WIRE-DISK-RQB RQB (SETQ QUANTUM NB)))
	(AND ( (SETQ TEM (// (- BLOCK ORIG-PART-BASE) 100.)) N-HUNDRED)
	     (FORMAT T "~D " (SETQ N-HUNDRED TEM)))
	(FUNCALL STREAM ':STRING-IN "Unexpected EOF" BUF 0 (* QUANTUM PAGE-SIZE 4))
	(SI:DISK-WRITE-WIRED RQB UNIT BLOCK))
      (STREAM-COPY-UNTIL-EOF STREAM 'IGNORE)
      (FUNCALL STREAM ':CLOSE)
      (SI:UPDATE-PARTITION-COMMENT PARTITION PARTITION-COMMENT UNIT))
    (AND RQB (RETURN-DISK-RQB RQB))))


(DEFUN READ-BAND-FROM-LMI-TAPE (PARTITION &OPTIONAL (UNIT 0)
				&AUX TAPE-P PART-SIZE PARTITION-COMMENT PART-BASE)
  (IF (NUMBERP PARTITION)
      (SETQ PARTITION (FORMAT NIL "LOD~D" PARTITION)))
  (MULTIPLE-VALUE (PART-BASE PART-SIZE)
    (FIND-DISK-PARTITION-FOR-WRITE PARTITION NIL UNIT))
  (SETQ TAPE-P
	(FQUERY FORMAT:YES-OR-NO-QUIETLY-P-OPTIONS
		"Shall I use the local tape drive? (NO means listen on CHAOS) "))
  (WITH-OPEN-STREAM (STREAM (IF TAPE-P
				(OPEN-TAPE 0 ':RECORD-LENGTH 4096.)
				(LET ((CONN (CHAOS:LISTEN "TAPE")))
				  (AND (STRINGP CONN)
				       (FERROR NIL "Cannot connect: ~A" CONN))
				  (CHAOS:ACCEPT CONN)
				  (CHAOS:MAKE-STREAM CONN ':DIRECTION ':INPUT))))
    (IF (NEQ (PKG-BIND ""
	       (READ STREAM))
	     ':LMFL)
	(FERROR NIL "This tape is not a LMFL"))
    (LET* ((LMI-STORY (LET ((IBASE 10.))
			(PKG-BIND "" (READ STREAM))))
	   (LMI-PLIST (LOCF LMI-STORY)))
      ;;Extract lmi info
      (LET ((BYTE-SIZE (GET LMI-PLIST ':BYTE-SIZE))
	    (GIVEN-PART-SIZE (GET LMI-PLIST ':SIZE)))
	(OR (EQUAL BYTE-SIZE 16.)
	    (FERROR NIL "Byte size ~D is not 16." BYTE-SIZE))
	(SETQ PARTITION-COMMENT (GET LMI-PLIST ':COMMENT))
	(OR (FIXP GIVEN-PART-SIZE)
	    (FERROR NIL "Partition size not given in LMI info"))
	
	(OR ( GIVEN-PART-SIZE PART-SIZE)
	    (FERROR NIL "Does not fit in local partition, ~D>~D" GIVEN-PART-SIZE
		    PART-SIZE))
	(SETQ PART-SIZE GIVEN-PART-SIZE)))
    (FUNCALL STREAM ':CLEAR-INPUT)		;Flush end of this first record
    (WRITE-BAND UNIT PARTITION STREAM PART-SIZE PART-BASE PARTITION-COMMENT)))
