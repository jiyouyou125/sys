;;;-*- Mode:LISP; Package:SYSTEM-INTERNALS -*-
;;; (c) Copyright 1981, Symbolics, Inc.
;;; Distribution system

;;; Procedure for making a distribution band:
;;; Copy all sources onto the local file system.
;;; Boot the regular SCRC system, and do (PRESET-FOR-DISTRIBUTION), which
;;; puts the distribution files on local for site setup.
;;; Boot a system without the Canon, and do (SET-FOR-DISTRIBUTION).  This
;;; can then be saved as the distribution band.

;;; This is called to setup the local file system for making the distribution band
(DEFUN PRESET-FOR-DISTRIBUTION ()
  (WITH-OPEN-FILE (FILE "LOCAL:>DISTRIBUTION>DISTRIBUTION>HOSTS.TEXT" '(:PRINT))
    (FUNCALL FILE ':LINE-OUT "; Host table for Distribution systems")
    (FORMAT FILE "~%HOST DISTRIBUTION-LM1,	CHAOS ~O,USER,LISPM,LISPM,[LM1]~%"
	    CHAOS:MY-ADDRESS))
  (COPY-SITE-FILE "LOCAL:>DISTRIBUTION>DISTRIBUTION>DEFAULT-SITE.LISP"
		  "LOCAL:>LISPM>DEFAULT-SITE.LISP")
  (COPY-SITE-FILE "LOCAL:>DISTRIBUTION>DISTRIBUTION>SITE.LISP"
		  "LOCAL:>LISPM>SITE.LISP")
  (COPY-SITE-FILE "LOCAL:>DISTRIBUTION>DISTRIBUTION>HOSTS.TEXT"
		  "LOCAL:>CHAOS>HOSTS.TEXT")
  (COPY-SITE-FILE "LOCAL:>DISTRIBUTION>DISTRIBUTION>LMLOCS.LISP"
		  "LOCAL:>LMIO>LMLOCS.LISP")
  (READFILE "LOCAL:>LISPM>SITE.LISP")
  (FS:ADD-LOGICAL-PATHNAME-HOST "SYS" "LOCAL" LISPM-LOGICAL-PATHNAME-TRANSLATIONS)
  (RECOMPILE-SITE-FILES)
  )

(DEFUN COPY-SITE-FILE (FROM TO)
  (WITH-OPEN-FILE (FROM-STREAM FROM '(:IN))
    (WITH-OPEN-FILE (TO-STREAM TO '(:OUT))
      (STREAM-COPY-UNTIL-EOF FROM-STREAM TO-STREAM NIL)
      (CLOSE TO-STREAM)
      (FORMAT T  "~&~A copied to ~A~%" (FUNCALL FROM-STREAM ':TRUENAME)
				       (FUNCALL TO-STREAM ':TRUENAME)))))

;;; This is called in a cold booted SCRC system to setup for the distribution band
(DEFUN SET-FOR-DISTRIBUTION ()
  (SET-DEFAULT-SITE)
  (SETQ HOST-ALIST NIL)
  (SETQ MACHINE-LOCATION-ALIST (CDR MACHINE-LOCATION-ALIST))
  (SETQ FS:*PATHNAME-HOST-LIST* (DELQ SI:LOCAL-HOST FS:*PATHNAME-HOST-LIST*))
  (SETQ SI:ASSOCIATED-MACHINE "LOCAL")
  (ADD-INITIALIZATION "DISTRIBUTION-SYS"
		      '(FS:CHANGE-LOGICAL-PATHNAME-HOST "SYS" "LOCAL")
		      '(COLD)))

;;; This is called by in the distribution band also.  Its use is described in the
;;; software installation guide.
(DEFUN SET-DEFAULT-SITE ()
  (DELETE-INITIALIZATION "DISTRIBUTION-SYS" '(COLD))
  (SETQ USER-ID "LISPM")
  (LOAD "LOCAL:>LISPM>DEFAULT-SITE")
  (PRINT-DISK-LABEL)
  "Now do a (DISK-SAVE n)")

(DEFUN RECOMPILE-SITE-FILES ()
  (MAKE-SYSTEM 'SITE-INTERNAL ':COMPILE ':NOLOAD ':NOCONFIRM
	       ':NO-RELOAD-SYSTEM-DECLARATION))

(DEFSYSTEM SITE-INTERNAL
  (:PACKAGE SI)
  (:MODULE HOST-TABLE (("SYS: CHAOS; HOSTS" "SYS: IO; HSTTBL")) :PACKAGE CHAOS)
  (:COMPILE-LOAD ("SYS: SYS; DEFAULT-SITE"))
  (:COMPILE-LOAD (:GENERATE-HOST-TABLE HOST-TABLE))
  (:COMPILE-LOAD ("SYS: SYS; SITE"))
  (:COMPILE-LOAD ("SYS: IO; LMLOCS"))
  )
